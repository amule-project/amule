#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_INIT([aMule],[CVS],[admin@amule.org])
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE

AC_CONFIG_SRCDIR([src/amule.cpp])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

# No -O2 by default, removing -fomit since it causes problems
CFLAGS="`echo $CFLAGS | sed "s/-fomit-frame-pointer//g"`"
CXXFLAGS="`echo $CXXFLAGS | sed "s/-fomit-frame-pointer//g"`"

# System detection, need AC_CANONICAL_TARGET
case "${target_os}" in
"")
	SYS=unknown
	;;
darwin*)
	SYS=darwin
	CFLAGS="${CFLAGS} -no-cpp-precomp -D_INTL_REDIRECT_MACROS -DNOPCH";
	LDFLAGS="$LDFLAGS -bind_at_load"
	;;
openbsd*) 
	SYS=openbsd
	LIBS="$LIBS -L/usr/local/lib"
	X11LIBS="-lX11 -L/usr/X11R6/lib"
	CXXFLAGS=" $CXXFLAGS -D__OPENBSD__"
	if test "$DEBUG_FLAGS" != ""; then
		DEBUG_FLAGS="-Wall -g -fno-inline -D__DEBUG__"
	fi
	;;
*cygwin* | *mingw32*)
	SYS=win32
	;;
solaris*)
	RESOLV_LIB="-lresolv -lnsl"
	X11LIBS="-lX11"
	LIBS="$LIBS -lrt"
esac

AC_SUBST(RESOLV_LIB)
AC_SUBST(X11LIBS)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_CC
AC_PROG_CPP
if test x$SYS = xwin32; then
	AC_PROG_AWK
fi
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC

# Find the right ranlib, even when cross-compiling
# And for some other tools for cross compilation
AC_CHECK_TOOL(RANLIB, ranlib)
AC_CHECK_TOOL(STRIP, strip)
AC_CHECK_TOOL(AR, ar)
AC_CHECK_TOOL(LD, ld)

AC_ARG_ENABLE(ccache, [  --enable-ccache            enable ccache support for fast recompilation],
		ccache="$enableval", ccache="no")

# Add args to configure
AC_ARG_ENABLE(debug,   [  --disable-debug            disable additional debugging output],
              USE_DEBUG="$enableval", USE_DEBUG="yes")

# to avoid conflicting, use "" when it is not used, and compare it to "optimise" value
AC_ARG_ENABLE(optimize, [  --enable-optimize          enable code optimizing],
		USE_OPTIMIZE="$enableval")

AC_ARG_ENABLE(optimise, [  --enable-optimise          enable code optimizing (deprecated)],
		USE_OPTIMISE="$enableval", USE_OPTIMISE="no")

AC_ARG_ENABLE(profile, [  --enable-profile           enable code profiling],
		USE_PROFILE="$enableval", USE_PROFILE="no")

AC_ARG_ENABLE(static,  [  --enable-static            produce a statically linked executable],
              USE_DEBUG_STATIC="$enableval", USE_DEBUG_STATIC="no")

AC_ARG_ENABLE(monolithic, [  --disable-monolithic       disable building of the monolithic aMule app],
	MONOLITHIC="$enableval", MONOLITHIC="yes")

AC_ARG_ENABLE(amule-daemon,  [  --enable-amule-daemon      compile aMule daemon version],
             AMULE_DAEMON="$enableval", AMULE_DAEMON="no")

AC_ARG_ENABLE(amulecmd,  [  --enable-amulecmd          compile aMule command line client],
              BUILDAMULECMD="$enableval", BUILDAMULECMD="no")

AC_ARG_ENABLE(amulecmdgui,  [  --enable-amulecmdgui       compile aMule command line client (with GUI)],
             GUI="$enableval", GUI="no")

AC_ARG_ENABLE(webserver,  [  --enable-webserver         compile aMule WebServer],
              WEB="$enableval", WEB="no")

AC_ARG_ENABLE(webservergui,  [  --enable-webservergui      compile aMule WebServer (with GUI)],
              WEBGUI="$enableval", WEBGUI="no")

AC_ARG_ENABLE(amule-gui,  [  --enable-amule-gui         compile aMule remote GUI (EXPERIMENTAL)],
             AMULE_GUI="$enableval", AMULE_GUI="no")

AC_ARG_ENABLE(cas,  [  --enable-cas               compile C aMule Statistics],
              CAS="$enableval", CAS="no")

AC_ARG_ENABLE(wxcas,  [  --enable-wxcas             compile aMule GUI Statistics],
              WXCAS="$enableval", WXCAS="no")

AC_ARG_ENABLE(ed2k,  [  --disable-ed2k             don't compile aMule ed2k links handler],
              ED2K="$enableval", ED2K="yes")

AC_ARG_ENABLE(alc,  [  --enable-alc               compile aMuleLinkCreator GUI version],
              ALC="$enableval", ALC="no")
	      
AC_ARG_ENABLE(alcc,  [  --enable-alcc              compile aMuleLinkCreator for console],
              ALCC="$enableval", ALCC="no")

AC_ARG_ENABLE(systray, [  --disable-systray          disable taskbar icon support],
			systray="$enableval", systray="yes")

AC_ARG_ENABLE(utf8_systray, [  --enable-utf8-systray      use UTF-8 encoded strings in taskbar icon],
		utf8_systray="$enableval", utf8_systray="no")

AC_ARG_ENABLE(embedded_crypto,[  --disable-embedded-crypto  do not use embedded Crypto code],
		USE_EMBEDDED_CRYPTO="$enableval", USE_EMBEDDED_CRYPTO="yes")

AC_ARG_ENABLE(gsocket, [  --disable-gsocket          disable patched gsocket.c linking],
		USE_GSOCKET="$enableval", USE_GSOCKET="yes")

AC_ARG_ENABLE(kad_compile, [  --enable-kad-compile      compile Kademlia code],
		kad_compile="$enableval", kad_compile="no")

# This must be set after AC_ARG_* or it will be overwritten
if test x"$target_os" = xdarwin; then
	USE_GSOCKET=no
fi

# Checking for libs before all other stuff to break
# configure as soon as possible in case of a missing one
# to avoid losing to much time

# Sets gettext version.
AM_GNU_GETTEXT_VERSION(0.11.5)

# Check for zlib
CHECK_ZLIB

if test x$CAS = xyes -o x$WEB = xyes -o x$WEBGUI = xyes; then
# Check for gd
AM_OPTIONS_GDLIBCONFIG

AM_PATH_GDLIBCONFIG(2.0.0,GDLIBFOUND=1)

case "$GDLIBFOUND" in
1)	CAS_DEFS="-D__GD__"
	GDLIB_LIBS="$GDLIB_LIBS -lgd"
	;;
*)AC_MSG_NOTICE([
	WARNING: gd-lib >= 2.0.0, or a functional gd.h (from gd-devel) not found.
	Please check that gdlib-config is in your default path, check out
	LD_LIBRARY_PATH or equivalent variable.
	Or this might also be that your gdlib-config has other name.
	Please try again with --with-gdlib-config=/usr/bin/gdlib-config
        (replace /usr/bin/gdlib-config with a valid path to your gdlib-config).
        If you use compiled packages check if you have devel pack installed.
        To download the latest version check http://www.boutell.com/gd
	for sources.
        ])
	
	CAS_DEFS=""
	GDLIB_LIBS=""
	GDLIB_CFLAGS=""
	GDLIB_LDFLAGS=""
	;;
esac

else
	CAS_DEFS=""
	GDLIB_LIBS=""
	GDLIB_CFLAGS=""
	GDLIB_LDFLAGS=""
	GDLIB_VERSION="not_needed"
fi

AC_SUBST(CAS_DEFS)


if test x$WEB = xyes -o x$WEBGUI = xyes; then
# Check for libpng
# This check _must_ occur *after* the check for gd
AM_OPTIONS_LIBPNGCONFIG

AM_PATH_LIBPNGCONFIG(1.2.0,LIBPNGFOUND=1)

case "$LIBPNGFOUND" in
1)	AMULEWEB_DEFS="-DWITH_LIBPNG"
	LIBPNG_LIBS="$LIBPNG_LIBS -lpng"
	;;
*)AC_MSG_NOTICE([
	WARNING: libpng-devel >= 1.2.0 is needed for amuleweb
        ])
	AMULEWEB_DEFS=""
	LIBPNG_LIBS=""
	LIBPNG_CFLAGS=""
	LIBPNG_LDFLAGS=""
	;;
esac
else
	AMULEWEB_DEFS=""
	LIBPNG_LIBS=""
	LIBPNG_CFLAGS=""
	LIBPNG_LDFLAGS=""
	LIBPNG_VERSION="not_needed"
fi

AC_SUBST(AMULEWEB_DEFS)


# Check if we need the GUI
AC_MSG_CHECKING([whether we need the GUI])
if test x"$MONOLITHIC" = x"yes" ; then
  need_gui="yes"
fi

if test x"$WXCAS" = x"yes" ; then
  need_gui="yes"
fi

if test x"$ALC" = x"yes" ; then
  need_gui="yes"
fi

if test x"$AMULE_GUI" = x"yes" ; then
  need_gui="yes"
fi

if test x"$GUI" = x"yes" ; then
  need_gui="yes"
fi

if test x"$WEBGUI" = x"yes" ; then
  need_gui="yes"
fi

if test "x${need_gui:+set}" != "xset" ; then
  need_gui="no"
fi
AC_MSG_RESULT($need_gui)


# Check for wx
AM_OPTIONS_WXCONFIG
AM_OPTIONS_WXBASECONFIG
AM_PATH_WXCONFIG(2.4.2)
AM_WXCONFIG_LARGEFILE

GET_WX_VERSION
if test $WX_VERSION_MAJOR -eq 2 -a $WX_VERSION_MINOR -eq 4; then
  WX_VERSION="2.4"
else
  WX_VERSION="any"
fi


havegtk="not_needed"
if test "x${need_gui}" = "xyes" ; then

	# Get toolkit name
	AC_MSG_CHECKING([for GUI toolkit to use])
	WXTOOLKIT=`$WX_CONFIG_WITH_ARGS --cflags | sed -e 's/-D__WXDEBUG__//' -e 's/-D__WXUNIVERSAL__//' -e 's/.*-D__WX\([[^[:blank:]]]*\)__.*/\1/'`
	if test -n "`$WX_CONFIG_WITH_ARGS --cflags | grep __WXUNIVERSAL__`"; then
		result="${WXTOOLKIT},UNIVERSAL"
	else
		result=$WXTOOLKIT
	fi
	AC_MSG_RESULT($result)

	if test x$WXTOOLKIT = xGTK; then

		CHECK_WX_BUILT_WITH_GTK2

		# Put this here instead of both in gtk.m4 and gtk-2_0.m4 so that it only appears once in configure --help
		AC_ARG_ENABLE(gtktest, [  --disable-gtktest          do not try to compile and run a test GTK program],
			, enable_gtktest=yes)

		if test x"$GTK_USEDVERSION" = x"2" ; then
			if test x"$WX_VERSION" = x"2.4" ; then
    			AC_MSG_ERROR([
				Your copy of wxWidgets 2.4.x is built against GTK2. This 
				combination is not supported. If you wish to make use of
				GTK2, then use the stable 2.6.x branch of wxWidgets,
				otherwise compile wxWidgets against GTK1,
				])
			fi
		
			AM_PATH_GTK_2_0(2.0.3, havegtk=2, havegtk=no)
			GTK_DEFS="-D__GTK2__"
			GET_GTK2_VERSION
		else		
			AM_PATH_GTK(1.2.0, havegtk=1, havegtk=no)
			GTK_DEFS=""
			GET_GTK_VERSION
		fi
	fi

else

	GTK_DEFS=""
	GTK_CFLAGS=""
	GTK_LIBS=""
	AC_SUBST(GTK_CFLAGS)
	AC_SUBST(GTK_LIBS)
	havegtk="not_needed"
	if test "x${systray}" = "xyes" ; then
		AC_MSG_WARN([Disabling taskbar icon support as we have no GUI.])
	fi
	systray=no
	utf8_systray=no

fi


dnl Get flags for windres
if test x$SYS = xwin32; then
	RCFLAGS=`$WX_CONFIG_WITH_ARGS --cflags | $AWK ['{ for (i = 1; i <= NF; i++) if (index($i, "-I") == 1) sub("-I", "--include-dir ", $i); else if (index($i, "-D") == 1) sub("-D", "--define ", $i); else $i = ""; print }']`
else
	RCFLAGS=""
fi
AC_SUBST(RCFLAGS)

# Check for Crypto++
AM_OPTIONS_CRYPTO

CHECK_CRYPTO(5.1)

case "$CRYPTO_PP_STYLE" in
 
 gentoo_debian) CXXFLAGS="$CXXFLAGS -I$crypto_prefix/include -D__CRYPTO_DEBIAN_GENTOO__"
                # Debian uses libcrypto++5.1 - it's not my fault, please soda patch the package
                LDFLAGS="$LDFLAGS -L$crypto_prefix/lib"
		CRYPTOLIBS="-lcrypto++"
 ;;
 mdk_suse_fc) CXXFLAGS="$CXXFLAGS -I$crypto_prefix/include -D__CRYPTO_MDK_SUSE_FC__"
              LDFLAGS="$LDFLAGS -L$crypto_prefix/lib"
	      CRYPTOLIBS="-lcryptopp"
 ;;
 sources) CXXFLAGS="$CXXFLAGS -I$crypto_prefix -D__CRYPTO_SOURCE__"
	  LDFLAGS="$LDFLAGS -L$crypto_prefix/cryptopp"
          CRYPTOLIBS="-lcryptopp"
 ;;
 embedded) CXXFLAGS="$CXXFLAGS -DUSE_EMBEDDED_CRYPTO"
 ;;
 *) AC_MSG_ERROR([
          WARNING: crypto++ >= $min_crypto_version is not found.
          Please check that cryptopp-headers are in your default include path,
	  check out LD_LIBRARY_PATH or equivalent variable.
          Or this might also be that your cryptopp is installed on other path.
          Please try again with --with-crypto-prefix=/my_crypto_prefix
          (replace /my_crypto_prefix with a valid path to your crypto directory).
          The crypto include folder must be named cryptopp.
	  Sorry about the mess but it's the only way because libcrypto has no make install.
          To download the latest version check http://www.cryptopp.com for sources.
	  ])
 ;;
esac

AC_SUBST(CRYPTOLIBS)

# Check for bfd.h (to have proper backtraces without using wxExecute)
CHECK_BFD

# Ok, we have all needed libs, now checking for other stuff

CHECK_AUTOPOINT

# Flush standard test results for speeding cache
AC_CACHE_SAVE

dnl -------------------------------------------------------------------------------
dnl Start of autoscan stuff
dnl -------------------------------------------------------------------------------
# Checks for header files.
AC_PATH_X
AC_FUNC_ALLOCA
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([argz.h arpa/inet.h fcntl.h inttypes.h langinfo.h libintl.h limits.h locale.h malloc.h mntent.h netdb.h netinet/in.h stddef.h stdint.h stdio_ext.h stdlib.h string.h strings.h sys/ioctl.h sys/mount.h sys/param.h sys/socket.h sys/statvfs.h sys/time.h unistd.h])
AC_HEADER_SYS_WAIT

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_TYPE_UID_T
AC_C_VOLATILE
AC_CHECK_TYPES([ptrdiff_t])

# Checks for library functions.
AC_FUNC_CLOSEDIR_VOID
AM_GNU_GETTEXT
AC_FUNC_GETMNTENT
AC_PROG_GCC_TRADITIONAL
AC_FUNC_FORK
if test x$SYS != xwin32; then
AC_FUNC_LSTAT
AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
fi
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_FUNC_SETVBUF_REVERSED
AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRFTIME
AC_FUNC_STRTOD
AC_CHECK_FUNCS([__argz_count __argz_next __argz_stringify endpwent floor ftruncate getcwd gethostbyaddr gethostbyname gethostname getmntent getmntinfo getpass gettimeofday inet_ntoa memmove mempcpy memset munmap nl_langinfo select setlocale socket sqrt stpcpy strcasecmp strchr strcspn strdup strerror strncasecmp strstr strtoul])
#-------------------------------------------------------------------------------
# End of autoscan stuff
#-------------------------------------------------------------------------------

dnl
dnl autoscan has missed these ones. Maybe because it's win32 stuff?
dnl
AC_CHECK_HEADERS([sys/resource.h sys/timeb.h])
AC_CHECK_FUNCS([mkdir getrlimit setrlimit])

# Flush standard test results for speeding cache
AC_CACHE_SAVE

# Other tests

case "$USE_DEBUG" in
yes)	DEBUG_FLAGS="-W -Wall -g -ggdb -fno-inline -D__DEBUG__ -fmessage-length=0 -D_GLIBCXX_DEBUG -D_GLIBCXX_DEBUG_PEDANTIC"
    ;;
*)	DEBUG_FLAGS=""
    ;;
esac

# wx version dependent stuff
case "$WX_VERSION_FULL" in
2.4.*)
	USE_24GSOCKET="yes"
	if test x$AMULE_DAEMON = xyes; then
	 NEEDSWX25="yes"
	 AC_MSG_NOTICE([WARNING: aMuled cannot be used with wxWidgets-2.4: disabling it])
	fi
	AMULE_DAEMON="no"
	gtk_tray="yes"
	;;
2.5.1)
	USE_24GSOCKET="no"
	gtk_tray="yes"
	;;
2.5.2)
	USE_24GSOCKET="no"
	gtk_tray="no"
	havegtk="not_needed"
	;;
*)
	USE_GSOCKET="no"
	USE_24GSOCKET="no"
	gtk_tray="no"
	havegtk="not_needed"
	;;
esac

#aMule options

# systray disable
AC_MSG_CHECKING([whether taskbar icon support should be compiled])
AC_MSG_RESULT($systray)

if test x$systray = xno; then
	TRAY_FLAGS="-D__SYSTRAY_DISABLED__"
else
	#systray type (GTK/WX)
	if test x$gtk_tray = xno; then
		TRAY_FLAGS="-DUSE_WX_TRAY"
	else
		# utf8_systray enable
		AC_MSG_CHECKING([whether taskbar icon should use UTF-8 strings])
		AC_MSG_RESULT($utf8_systray)

		if test x$utf8_systray = xyes; then
			TRAY_FLAGS="-D__UTF8_SYSTRAY_ENABLED__"
		fi
	fi
fi
AC_SUBST(TRAY_FLAGS)

AC_SUBST(GTK_DEFS)

# Command line tools
case "$BUILDAMULECMD" in
yes)	BUILD_AMULECMD="yes"
	;;
*)	BUILD_AMULECMD="no"
	;;
esac


case "$WXBASE24FOUND" in
1)	WXBASE_CFLAGS="$WXBASE_CFLAGS -DwxUSE_GUI=0"
	WXBASE_CXXFLAGS="$WXBASE_CXXFLAGS -DwxUSE_GUI=0"
	;;
*)	if test "$WXBASE25FOUND" = 1; then
		WXBASE_CFLAGS="$WXBASE_CFLAGS -DwxUSE_GUI=0"
		WXBASE_CXXFLAGS="$WXBASE_CXXFLAGS -DwxUSE_GUI=0"
	else
		( test x"$WEB" = x"yes" || test x"$ALCC" = x"yes" || test x"$BUILDAMULECMD" = x"yes" ) && NEEDSWXBASE="yes"
		BUILD_AMULECMD="no"
		WEB="no"
		ALCC="no"
	fi
	;;
esac

# Optimization
if test x"$USE_OPTIMISE" = x"yes" -a x"$USE_OPTIMIZE" = x; then
	USE_OPTIMIZE=yes
fi

case "$USE_OPTIMIZE" in

yes)	OPTIMIZE_FLAGS="-O2"
	;;
*)	OPTIMIZE_FLAGS=""
	;;
esac

# Profiling
case "$USE_PROFILE" in
yes)	PROFILE_FLAGS="-pg"
	;;
*)	PROFILE_FLAGS=""
	;;
esac

AC_MSG_CHECKING([if the applications should be statically linked])
case "$USE_DEBUG_STATIC" in
yes)	LDFLAGS="$LDFLAGS -static"
	;;
esac
AC_MSG_RESULT($USE_DEBUG_STATIC)

if test "x$prefix" != "xNONE"; then
	aMuleInstallPrefix=$prefix
else
	aMuleInstallPrefix=$ac_default_prefix
fi
AC_DEFINE_UNQUOTED(AMULE_INSTALL_PREFIX, "${aMuleInstallPrefix}", [aMule installation prefix])

AC_DEFINE_UNQUOTED(AMULE_LOCALEDIR, "$datadir/locale", [Define where the locales are.])
		
# Try to set the CVS checkout-date
if test -f "$srcdir/CVSDATE" # CVSDATE for tarballs
then
	cvsdate=`cat $srcdir/CVSDATE`
elif test -f "$srcdir/CVS/Entries" # CVSDATE for cvs
then
	cvsdate="$(stat -Lc %y $srcdir/CVS/Entries | sed -e 's#\.[0-9]*##')" >/dev/null 2>&1
elif test -d $srcdir/.svn # CVSDATE for svn
then
	cvsdate="r`svnversion $srcdir | sed -e 's/[[0-9]][[0-9]]*:\(.*\)/\1/' -e 's/\([[0-9]][[0-9]]*\).*/\1/'`"
fi
test "$cvsdate" && AC_DEFINE_UNQUOTED(CVSDATE, "$cvsdate", [The checkout date or svn revision number.])

XRCFLAGS="-Isrc"
CXXFLAGS="$CXXFLAGS $DEBUG_FLAGS $OPTIMIZE_FLAGS $PROFILE_FLAGS"
CFLAGS="$CFLAGS $DEBUG_FLAGS $OPTIMIZE_FLAGS $PROFILE_FLAGS"

LDFLAGS="$LDFLAGS $PROFILE_FLAGS"
dnl -lpthread is needed by Debian but FreeBSD < 5 doesnt support it
if test x$SYS != xwin32; then
	AC_MSG_CHECKING([if this is a FreeBSD 4 or earlier system])
	if test x"`uname -s`" = xFreeBSD && test 0`uname -r | cut -c 1` -lt 5; then
		LDFLAGS="$LDFLAGS -pthread"
		AC_MSG_RESULT(yes)
	else
		LDFLAGS="$LDFLAGS -lpthread"
		AC_MSG_RESULT(no)
	fi
fi

case "$USE_NLS" in

yes)
	;;
*)
	HAVE_GETTEXT="no"
esac


AM_CONDITIONAL(USE_EMBEDDED_CRYPTO, test x$USE_EMBEDDED_CRYPTO = xyes)
AM_CONDITIONAL(WEB, test x$WEB = xyes)
AM_CONDITIONAL(ED2K, test x$ED2K = xyes)
AM_CONDITIONAL(CAS, test x$CAS = xyes)
AM_CONDITIONAL(WXCAS, test x$WXCAS = xyes)
AM_CONDITIONAL(ALC, test x$ALC = xyes)
AM_CONDITIONAL(ALCC, test x$ALCC = xyes)
AM_CONDITIONAL(GUI, test x$GUI = xyes)
AM_CONDITIONAL(AMULE_GUI, test x$AMULE_GUI = xyes)
AM_CONDITIONAL(AMULE_DAEMON, test x$AMULE_DAEMON = xyes)
AM_CONDITIONAL(WEBGUI, test x$WEBGUI = xyes)
AM_CONDITIONAL(COMPILE_CMD, test x$BUILD_AMULECMD = xyes)
AM_CONDITIONAL(ADD_24GSOCKET, test x$USE_24GSOCKET = xyes)
AM_CONDITIONAL(ADD_GSOCKET, test x$USE_GSOCKET = xyes)
AM_CONDITIONAL(HAVE_TRAY, test x$systray = xyes)
AM_CONDITIONAL(HAVE_OLD_TRAY, test x$gtk_tray = xyes)
AM_CONDITIONAL(MONOLITHIC, test x$MONOLITHIC = xyes)
AM_CONDITIONAL(NEED_RC, test x$SYS = xwin32)
AM_CONDITIONAL(COMPILE_KAD, test x$kad_compile = xyes)
AM_CONDITIONAL(COMPILE_NLS, test x$HAVE_GETTEXT = xyes)

dnl
dnl Check for readline library
dnl
if test x$BUILDAMULECMD = xyes -o x$GUI = xyes -o x$WEB = xyes -o x$WEBGUI = xyes; then
	VL_LIB_READLINE
fi

# DO NOT MOVE UP...THAT'S A REASON TO BE HERE!!

AM_OPTIONS_CCACHE_PFX

AC_MSG_CHECKING([whether ccache support should be added])
AC_MSG_RESULT($ccache)
AC_SUBST(ccache)

case "$ccache" in

yes)
	CHECK_CCACHE
	if [[ $CCACHE = 0 ]]; then
	GCC="$ccache_prefix/ccache $GCC"
	CC="$ccache_prefix/ccache $CC"
	GXX="$ccache_prefix/ccache $GXX"
	CXX="$ccache_prefix/ccache $CXX"
	else
	GCC="$GCC"
	CC="$CC"
	GXX="$GXX"
	CXX="$CXX"
	fi
	;;
*)      GCC="$GCC"
	CC="$CC"
	GXX="$GXX"
	CXX="$CXX"
esac

dnl
dnl Generate the Makefiles
dnl
AC_CONFIG_FILES([Makefile
		 docs/Makefile
		 docs/man/Makefile
		 intl/Makefile
		 m4/Makefile
		 src/Makefile
		 src/kademlia/Makefile
		 src/kademlia/utils/Makefile
		 src/kademlia/io/Makefile
		 src/kademlia/kademlia/Makefile
		 src/kademlia/routing/Makefile
		 src/kademlia/net/Makefile
		 src/pixmaps/Makefile
		 src/utils/Makefile
		 src/utils/aLinkCreator/Makefile
		 src/utils/aLinkCreator/docs/Makefile
		 src/utils/aLinkCreator/src/Makefile
		 src/utils/cas/Makefile
		 src/utils/cas/docs/Makefile
		 src/utils/wxCas/Makefile
		 src/utils/wxCas/docs/Makefile
		 src/utils/wxCas/src/Makefile
		 src/utils/xas/Makefile
		 src/utils/xas/docs/Makefile
		 src/webserver/Makefile
		 src/webserver/src/Makefile
		 src/webserver/default/Makefile
		 src/webserver/php-default/Makefile
		 src/webserver/chicane/Makefile
		 unittests/Makefile
		 unittests/muleunit/Makefile
		 unittests/tests/Makefile])
dnl
dnl autoscan has missed these, i wonder why...
dnl
AC_CONFIG_FILES([po/Makefile.in
		 Compilation.flags])
AC_OUTPUT

echo
echo
echo "  Configure script has finished system check."
echo
echo "  Configured aMule ${PACKAGE_VERSION} for '${host}'."
echo
echo "  aMule enabled options:"
echo 
echo "  **** aMule Core ****" 
echo "  Prefix where aMule should be installed?                    ${prefix:-none}"
echo "  Should aMule be compiled with i18n support?                ${HAVE_GETTEXT:-no}"
echo "  Should aMule be compiled in debug mode?                    ${USE_DEBUG:-no}"
echo "  Should aMule be compiled with profiling?                   ${USE_PROFILE:-no}"
echo "  Should aMule be compiled with optimizations?               ${USE_OPTIMIZE:-no}"
echo "  Should aMule be compiled with taskbar icon support?        ${systray:-yes}"
if test x$systray != xno; then
	echo "  Should aMule be compiled with old GTK taskbar icon?        ${gtk_tray:-no}"
	if test x$gtk_tray = xyes; then
		echo "  Should aMule be compiled with UTF-8 taskbar icon support?  ${utf8_systray:-no}"
	fi
fi
echo "  Should aMule be linked against patched gsocket?            ${USE_GSOCKET:-no}"
echo "  Should aMule monolithic application be built?              ${MONOLITHIC:-yes}"
echo "  Should aMule daemon version be built?                      ${AMULE_DAEMON:-no}"
echo "  Should aMule remote gui be built? (EXPERIMENTAL)           ${AMULE_GUI:-no}"
echo "  Should aMule Kademlia code be compiled?                    ${kad_compile:-no}"
echo "  Crypto++ library/headers style?                            ${CRYPTO_PP_STYLE:-embedded}"

echo
echo "  **** aMule TextClient ****"
echo "  Should aMule Command Line Client be built?                 ${BUILD_AMULECMD:-no}"
echo "  Should aMule GUI Client be built?                          ${GUI:-no}"
echo
echo "  **** aMule WebServer ****"
echo "  Should aMule WebServer be built?                           ${WEB:-no}"
echo "  Should aMule WebServer GUI be built?                       ${WEBGUI:-no}"
echo
echo "  **** aMule ED2K Links Handler ****"
echo "  Should aMule ED2K Links Handler be built?                  ${ED2K:-yes}"
echo
echo "  **** aMuleLinkCreator ****"
echo "  Should aMuleLinkCreator GUI version (alc) be built?        ${ALC:-no}"
echo "  Should aMuleLinkCreator for console (alcc) be built?       ${ALCC:-no}"
echo
echo "  **** aMule Statistics ****"
echo "  Should C aMule Statistics (CAS) be built?                  ${CAS:-no}"
echo "  Should aMule GUI Statistics (wxCas) be built?              ${WXCAS:-no}"

echo
echo "  **** General Libraries and Tools ****"
echo "  Should ccache support be enabled?                          ${ccache:-no}"
echo "  Libraries aMule will use to build:"
echo "                                       wxWidgets             ${WX_VERSION_FULL:-Not detected}"
if test x"$havegtk" != x"not_needed"; then
	echo "                                       GTK                   ${GTK_VERSION:-Not detected}"
fi
echo "                                       crypto++              ${crypto_version:-embedded}"
if test x$LIBPNG_VERSION != xnot_needed; then
	echo "                                       libpng                ${LIBPNG_VERSION:-Not detected}"
fi
if test x$GDLIB_VERSION != xnot_needed; then
	echo "                                       libgd                 ${GDLIB_VERSION:-Not detected}"
fi
echo "                                       zlib                  ${ZLIB_VERSION:-Not detected}"

if test x"$NEEDSWX25" = x"yes" ; then
echo ""
echo "  NOTICE: You need wxWidgets 2.5 or later to compile:"
echo "          amuled"
fi

if test x"$NEEDSWXBASE" = x"yes" ; then
echo ""
echo "  NOTICE: With wxWidgets versions prior to 2.5, you need wxBase library to compile:"
echo "          amuleweb amulecmd alcc"
fi

if test x"$LIBPNG_VERSION" = x""; then
echo ""
echo ""
echo " WARNING! libpng >= 1.2.0 not found."
echo " amuleweb will be compiled but file download progress bar images will NOT be generated."
echo ""
fi
